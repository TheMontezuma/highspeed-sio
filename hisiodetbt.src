;  highsiodet.src - check for highspeed drives
;
;  Copyright (c) by Matthias Reichl <hias@horus.com>
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
;

HISIO

        CLC
        LDA DDEVIC  ;device ID
        ADC DUNIT   ;add unit number
        ADC #$FF    ;subtract 1
        STA CDEVIC  ;device bus ID

?DOSIO
        JSR ?GETHSINDEX
        BEQ ?DOCHK

        SEC     ; bytes in SIO table are now speed+1
        SBC #1  ; to prevent endless loop in case device sends speed byte 0
        STA SIOSPEED
        JMP DOHISIO

?DOCHK
        LDA SOUNDR
        PHA
        LDA #0
        STA SOUNDR

        LDY #$09
?SAVCMD
        LDA DCOMND,Y
        PHA         ; backup original DCB
        LDA ?CMD3F,Y
        STA DCOMND,Y
        DEY
        BPL ?SAVCMD

        LDA #BT576K ; test divisor $08 (SIO2BT 57.6Kbps)
        JSR ?GETST
        BPL ?SETSPD
        CPY #ERRNAK
        BEQ ?SETSPD

        LDA #STDSPD
        JSR ?GETST
        BPL ?TESTHISIO
        CPY #ERRNAK
        BEQ ?TESTHISIO
        LDA #$FF    ; don't set speed if device timed out
        STA SIOSPEED
        BNE ?RESCMD

?TESTHISIO
        LDY #$09        ; reinstate the speed poll command since get status wiped it out
?SAVCMD2
        LDA ?CMD3F,Y    ; Happy/Speedy Ultra SIO mode?
        STA DCOMND,Y
        DEY
        BPL ?SAVCMD2
        JSR ?DODET

        BMI ?NOULTRA

        LDA HIBUF
        CMP #$0A        ; speed byte $0A is most certainly a Happy 1050
        BNE ?SETSP2

; enable fast write for Happy 1050 (but not for other drives),
; otherwise data might be corrupted
; stupid, stupid Happy...

        JSR ?CHKHAP
        LDA HIBUF
        JMP ?SETSP2

?NOULTRA
        LDA #TURFLG ; Turbo 1050?
        JSR ?GETST
        BPL ?SETSPD

        LDA #XFFLG  ; XF551?
        JSR ?GETST
        BPL ?SETSPD

        LDA #HAPFLG ; Happy 810?
        STA SIOSPEED
        JSR ?CHKHAP
        BPL ?SETSPD

        LDA #STDSPD ; no, just use standard SIO speed
        BNE ?SETSP2

?GETST
        STA SIOSPEED
        LDA #4
        STA DBYTLO
        LDX #$53
        LDA #$40
?DOIMM  STX DCOMND
        STA DSTATS
?DODET  LDA #1
        LDX #DETCRETRY
        JMP DOHIDET

?CHKHAP LDX #$48    ; check for happy 810
        LDA #0
        BEQ ?DOIMM

?SETSPD
        LDA SIOSPEED
?SETSP2
        JSR ?STOREHSINDEX

?RESCMD LDY #$00    ; restore original DCB
?RESLP  PLA
        STA DCOMND,Y
        INY
        CPY #$0A
        BCC ?RESLP
        PLA
        STA SOUNDR

        LDA SIOSPEED
        CMP #$FF
        BNE ?DETEND
        LDA #ERRTO  ; if speed byte is $FF, drive is offline so just return NAK
        STA STATUS
        JMP ?NOWRAP
?DETEND JMP ?DOSIO

; Retrieve HSIndex for CDEVIC
; Returns HSIndex+1 in A or 0 in zero flag if not present
?GETHSINDEX
        LDY #SPEEDTBSIZE
?Loop   LDA DEVICETB-1,Y
        CMP CDEVIC
        BEQ ?Found
        DEY
        BNE ?Loop
        RTS
?Found  LDA SPEEDTB-1,Y ; found device, so get HSIndex
        RTS

; Store HSIndex for CDEVIC
; Oldest entries are overwritten
; Pass speed in A
?STOREHSINDEX
        CLC     ; add 1 to value to prevent endless loop
        ADC #1  ; if device sends a speed byte of 0
        LDY NEXTSPEEDSLOT   ; this will be 0-3
        STA SPEEDTB,Y
        LDA CDEVIC
        STA DEVICETB,Y
        INY
        TYA
        AND #SPEEDTBSIZE-1
        STA NEXTSPEEDSLOT
        RTS

?CMD3F  .BYTE $3F,$40
        .WORD HIBUF,1,1,$20
